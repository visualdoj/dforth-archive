// 2011.08.07 # Doj
// Реализация меток для условного и безусловного переходов

include" units\asm\i386\core.de"
include" units\structures\ndyn.de"

I386 extend-namespace 
  namespace LABELS
    0
      t_str -- .marker
      t_ptr -- .pos
    struct s_marker
    16 s_marker ndyn-global M
    
    // Делает контейнер M пустым
    : M-clear
      M# begin ?dup while 1-
        dup M[] .marker 0 w>b b!
      repeat
      0 M#! ;

    0
      t_str -- .label
      t_ptr -- .ptr
      t_ptr -- .xt
    struct s_label
    16 s_label ndyn-global L

    // Делает контейнер L пустым
    : L-clear
      L# begin ?dup while 1-
        dup L[] .label 0 w>b b!
      repeat
      0 L#! ;

    : M+ ( B: S)
        M0+ M[#] .marker b!
        pos@ M[#] .pos ! ;
    : @> ( "name") source-next-name M+ ;
    : label+ ( B: s -- l) L0+ L[#] dup .label b! ;
    : @@ ( "name" --) source-next-name label+ link ;
    : label! ( pxl) tuck .xt ! .ptr ! ;

    : label-resolve ( pos label) dup .ptr @ swap .xt @ execute ;
    ( а оно нам нужно? 
      B: s -- l
      : find-label
          L# begin dup while 1- 
            dup L[] .label bdup b@ str= if bdrop L[] exit then
          repeat bdrop ; )
    : find-marker // B: s -- l
          M# begin dup while 1- 
            dup M[] .marker bdup b@ str= if bdrop M[] exit then
          repeat bdrop ;
    : all-resolve
        L# begin ?dup while 1- dup
          dup L[] .label b@ find-marker ?dup if
            .pos @ over L[] label-resolve
          else
            " cannot resolve marker '" dup L[] .marker b@ str+ +" '" error
          then
        repeat ;

    ' byte! value 'byte-resolver
    ' word! value 'word-resolver
    ' ! value 'dword-resolver
    : byte-rel-resolver ( val ptr) tuck - swap byte! ;
    last value 'byte-rel-resolver
    : word-rel-resolver ( val ptr) tuck - swap word! ;
    last value 'word-rel-resolver
    : dword-rel-resolver ( val ptr) tuck - swap dword! ;
    last value 'dword-rel-resolver
    : byte-rel-resolver1- ( val ptr) tuck - 1- swap byte! ;
    last value 'byte-rel-resolver1-
    : word-rel-resolver1- ( val ptr) tuck - 1- swap word! ;
    last value 'word-rel-resolver1-
    : dword-rel-resolver1- ( val ptr) tuck - 1- swap dword! ;
    last value 'dword-rel-resolver1-
    : byte-rel-resolver2- ( val ptr) tuck - 2 - swap byte! ;
    last value 'byte-rel-resolver2-
    : word-rel-resolver2- ( val ptr) tuck - 2 - swap word! ;
    last value 'word-rel-resolver2-
    : dword-rel-resolver2- ( val ptr) tuck - 2 - swap dword! ;
    last value 'dword-rel-resolver2-
  \namespace

  LABELS
    samealias 'dword-resolver
    samealias 'word-resolver
    samealias 'byte-resolver
    samealias 'dword-rel-resolver
    samealias 'word-rel-resolver
    samealias 'byte-rel-resolver
    samealias 'dword-rel-resolver1-
    samealias 'word-rel-resolver1-
    samealias 'byte-rel-resolver1-
    samealias 'dword-rel-resolver2-
    samealias 'word-rel-resolver2-
    samealias 'byte-rel-resolver2-
    samealias @>
    samealias @@
    samealias all-resolve
  ^
\namespace
