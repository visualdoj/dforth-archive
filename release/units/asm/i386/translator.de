// 2011.07.31 # Doj
// Транслятор

include" units\asm\i386\description.de"

I386 extend-namespace 
  namespace COMMANDS
    uses COMMAND_DESCRIPTION
      include" units\asm\i386\tables\ia32.de"
      include" units\asm\i386\tables\fpu.de"
      include" units\asm\i386\tables\mmx.de"
      include" units\asm\i386\tables\sse.de"
      include" units\asm\i386\tables\sse2.de"
      include" units\asm\i386\tables\sse3.de"
      include" units\asm\i386\tables\sse4.1.de"
      include" units\asm\i386\tables\sse4.2.de"
    \uses
  \namespace
\namespace

I386 extend-namespace 
uses COMMAND_DESCRIPTION COMMANDS
  namespace TRANSLATOR
    // default
    *auto-integers? off

    // список кратких регистров
    : short-reg32 create , does> @ reg32 ;
    EAX short-reg32 eax
    ECX short-reg32 ecx
    EDX short-reg32 edx
    EBX short-reg32 ebx
    ESP short-reg32 esp
    EBP short-reg32 ebp
    ESI short-reg32 esi
    EDI short-reg32 edi
    : short-reg16 create , does> @ reg16 ;
    AX short-reg16 ax
    CX short-reg16 cx
    DX short-reg16 dx
    BX short-reg16 bx
    SP short-reg16 sp
    BP short-reg16 bp
    SI short-reg16 si
    DI short-reg16 di
    : short-reg8 create , does> @ reg8 ;
    AL short-reg8 al
    CL short-reg8 cl
    DL short-reg8 dl
    BL short-reg8 bl
    AH short-reg8 ah
    CH short-reg8 ch
    DH short-reg8 dh
    BH short-reg8 bh
    // список кратких сегментных регистров
    : short-seg create , does> @ seg ;
    ES short-seg es
    CS short-seg cs
    SS short-seg ss
    DS short-seg ds
    FS short-seg fs
    GS short-seg gs

// {{{ PREFIX
    namespace PREFIX
      variable *xt
      : \PREFIX ^^^ *xt @ execute ;
      : prefix-mnemonic ( p "name") create , does> @ PREFIX *xt ^ dup @ execute ! ;
    \namespace
    : prefix-reset PREFIX *xt ^ dup @ execute ['] nop swap ! ;
    : step-prefix-reset compile? if postpone prefix-reset else prefix-reset then ;
// }}}
// {{{ directives
    namespace DB
      : \db ^^^ ; immediate
      : _COMPILE_NOTFOUND_ str->int postpone literal postpone byte<< true ;
      : _NOTFOUND_ str->int byte<< true ;
    \namespace
    : db call DB step-prefix-reset ; immediate
    namespace DW
      : \dw ^^^ ; immediate
      : _COMPILE_NOTFOUND_ str->int postpone literal postpone word<< true ;
      : _NOTFOUND_ str->int byte<< true ;
    \namespace
    : dw call DW step-prefix-reset ; immediate
    namespace DD
      : \dd ^^^ ; immediate
      : _COMPILE_NOTFOUND_ str->int postpone literal postpone dword<< true ;
      : _NOTFOUND_ str->int byte<< true ;
    \namespace
    : dd call DD step-prefix-reset ; immediate
// }}}

    : mem<< ( rp-) 0 rrot .base @ ModR/M byte<< ;

// {{{ unary-op (INC, DEC)
    0
      t_int -- .reg
      t_int -- .ex
      t_int -- .r/m32
      t_int -- .r/m8
    struct s_unary
    : unary-op
      create , , , , does>
      1 ?operands
      first .type @ switch
        OT_MEM of 
                 first .value @ switch
                   DT_BYTE of dup .r/m8 @ byte<< .ex @ first mem<< \of
                   DT_DWORD of dup .r/m32 @ byte<< .ex @ first mem<< \of
                   ( error)
                 \switch
               \of
        OT_REG of .reg @ first .value @ + byte<< \of
        ( error)
      \switch
      clear-operands
    ;
   // }}}
    hFE hFF 0 h40 unary-op INC
    hFE hFF 1 h48 unary-op DEC

// {{{ NEG
    : unary-op
      create , , , , does>
      1 ?operands
      first .type @ switch
        OT_MEM of 
                 first .value @ switch
                   DT_BYTE of dup .r/m8 @ byte<< .ex @ first mem<< \of
                   DT_DWORD of dup .r/m32 @ byte<< .ex @ first mem<< \of
                   ( error)
                 \switch
               \of
        OT_REG of dup .r/m32 @ byte<< .ex @ first mem<< \of
        ( error)
      \switch
      clear-operands
    ;
    hF6 hF7 3 0 unary-op NEG
 // }}}
    h90 0op NOP
    hC2 w-op RET
    hC3 0op RET
// {{{ POP
    : POP
      1 ?operands
      first .type @ switch
        OT_REG of
                  first .regtype @ switch
                    RT_16 of COMMAND_DESCRIPTION POP16_reg ^ \of
                    RT_32 of COMMAND_DESCRIPTION POP32_reg ^ \of
                    unknown-operand-type
                  \switch \of
        OT_MEM of 
                  first .value @ switch
                    DT_WORD of COMMAND_DESCRIPTION POP16_mem ^ \of
                    DT_DWORD of COMMAND_DESCRIPTION POP32_mem ^ \of
                    unknown-operand-type
                  \switch \of
        OT_SEG of
                 first .value @ switch 
                   ES of COMMAND_DESCRIPTION POP_es ^ \of
                   SS of COMMAND_DESCRIPTION POP_ss ^ \of
                   DS of COMMAND_DESCRIPTION POP_ds ^ \of
                   FS of COMMAND_DESCRIPTION POP32_fs ^ \of
                   GS of COMMAND_DESCRIPTION POP32_gs ^ \of
                   " unsupported segment register" error
                 \switch \of
        unknown-operand-type
      \switch clear-operands ;
// }}}
// {{{ PUSH
    : PUSH
      1 ?operands
      first .type @ switch
        OT_IMM of
                 first .value @ 128 < // TODO : signed case
                 if 
                   COMMAND_DESCRIPTION PUSH8_imm ^
                 else
                   COMMAND_DESCRIPTION PUSH32_imm ^
                 then \of
        OT_REG of
                  first .regtype @ switch
                    RT_16 of COMMAND_DESCRIPTION PUSH16_reg ^ \of
                    RT_32 of COMMAND_DESCRIPTION PUSH32_reg ^ \of
                    unknown-operand-type
                  \switch \of
        OT_MEM of 
                  first .value @ switch
                    DT_WORD of COMMAND_DESCRIPTION PUSH16_mem ^ \of
                    DT_DWORD of COMMAND_DESCRIPTION PUSH32_mem ^ \of
                    unknown-operand-type
                  \switch \of
        OT_SEG of
                 first .value @ switch 
                   ES of COMMAND_DESCRIPTION PUSH_es ^ \of
                   CS of COMMAND_DESCRIPTION PUSH_cs ^ \of
                   SS of COMMAND_DESCRIPTION PUSH_ss ^ \of
                   DS of COMMAND_DESCRIPTION PUSH_ds ^ \of
                   FS of COMMAND_DESCRIPTION PUSH_fs ^ \of
                   GS of COMMAND_DESCRIPTION PUSH_gs ^ \of
                 \switch
               \of
        unknown-operand-type
      \switch clear-operands
    ;
// }}}
// {{{ XCHG
    : XCHG_reg_reg
      first .value @ EAX = if
        first .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION XCHG8_reg_reg ^ \of
          RT_16 of COMMAND_DESCRIPTION XCHG16_reg_ax ^ \of
          RT_32 of COMMAND_DESCRIPTION XCHG32_reg_eax ^ \of
          unknown-operand-type
        \switch exit then
      second .value @ EAX = if
        first .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION XCHG8_reg_reg ^ \of
          RT_16 of COMMAND_DESCRIPTION XCHG16_ax_reg ^ \of
          RT_32 of COMMAND_DESCRIPTION XCHG32_eax_reg ^ \of
          unknown-operand-type
        \switch exit then
      first .regtype @ switch
        RT_8  of COMMAND_DESCRIPTION XCHG8_reg_reg ^ \of
        RT_16 of COMMAND_DESCRIPTION XCHG16_reg_reg ^ \of
        RT_32 of COMMAND_DESCRIPTION XCHG32_reg_reg ^ \of
        unknown-operand-type
        \switch ;
    : XCHG_reg_mem
        second .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION XCHG8_reg_mem ^ \of
          RT_16 of COMMAND_DESCRIPTION XCHG16_reg_mem ^ \of
          RT_32 of COMMAND_DESCRIPTION XCHG32_reg_mem ^ \of
          unknown-operand-type
        \switch ;
    : XCHG_reg
      first .type @ switch
        OT_REG of XCHG_reg_reg \of
        OT_MEM of XCHG_reg_mem \of
        unknown-operand-type
      \switch clear-operands ;
    : XCHG_mem
      first .type @ OT_REG = if
        first .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION XCHG8_mem_reg ^ \of
          RT_16 of COMMAND_DESCRIPTION XCHG16_mem_reg ^ \of
          RT_32 of COMMAND_DESCRIPTION XCHG32_mem_reg ^ \of
          unknown-operand-type
        \switch
      else
        " cannot apply XCHG for two mem operands" error
      then ;
    : XCHG
      2 ?operands
      second .type @ switch
        OT_REG of XCHG_reg \of
        OT_MEM of XCHG_mem \of
        unknown-operand-type
      \switch clear-operands ;
// }}}
// {{{ MOV
// {{{ MOV reg
// {{{ MOV reg imm
    : MOV_reg_imm
      second .regtype @ switch
        RT_8  of COMMAND_DESCRIPTION MOV8_reg_imm ^ \of
        RT_16 of 16 switch<< COMMAND_DESCRIPTION MOV16_reg_imm ^ \of
        RT_32 of 32 switch<< COMMAND_DESCRIPTION MOV32_reg_imm ^ \of
      \switch
    ;
// }}}
// {{{ MOV reg reg
    : MOV_reg_reg
      first .regtype @ second .regtype @ <> 
      if " both operands of MOV must have the same type" error then
      second .regtype @ switch
        RT_8 of COMMAND_DESCRIPTION MOV8_reg_reg ^ \of
        RT_16 of 16 switch<< COMMAND_DESCRIPTION MOV16_reg_reg ^ \of
        RT_32 of 32 switch<< COMMAND_DESCRIPTION MOV32_reg_reg ^ \of
      \switch
    ;
// }}}
// {{{ MOV reg mem
    : MOV_reg_mem
      second .value @ EAX = first .base @ -1 = and first .factor @ 0= and if
        // особый случай
        second .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION MOV8_al_moffset ^ \of
          RT_16 of COMMAND_DESCRIPTION MOV16_ax_moffset ^ \of
          RT_32 of COMMAND_DESCRIPTION MOV32_eax_moffset ^ \of
        \switch
        exit
      then
      second .regtype @ switch
        RT_8  of COMMAND_DESCRIPTION MOV8_reg_mem ^ \of
        RT_16 of COMMAND_DESCRIPTION MOV16_reg_mem ^ \of
        RT_32 of COMMAND_DESCRIPTION MOV32_reg_mem ^ \of
      \switch
    ;
// }}}
// {{{ MOV reg seg
    : MOV_reg_seg 
      first .value @ DT_WORD = if
        16 switch<< COMMAND_DESCRIPTION MOV16_mem_seg ^
      else " mov to segment register must be 16-bit memory operand" error then
    ;
// }}}
    : MOV_reg
      first .type @ switch
        OT_IMM of MOV_reg_imm \of
        OT_REG of MOV_reg_reg \of
        OT_MEM of MOV_reg_mem \of
        OT_SEG of MOV_reg_seg \of
      \switch ;
// }}}
// {{{ MOV mem
    : MOV_mem_imm
      second .value @ switch
        DT_BYTE of COMMAND_DESCRIPTION MOV8_mem_imm ^ \of
        DT_WORD of ( 16 switch<<) COMMAND_DESCRIPTION MOV16_mem_imm ^ \of
        DT_DWORD of ( 32 switch<<) COMMAND_DESCRIPTION MOV32_mem_imm ^ \of
        unknown-operand-type
      \switch
    ;
    : MOV_mem_reg 
      first .value @ EAX = second .base? @ not and second .factor @ 0= and if
        // особый случай
        first .regtype @ switch
          RT_8 of COMMAND_DESCRIPTION MOV8_moffset_al ^ \of
          RT_16 of 16 switch<< COMMAND_DESCRIPTION MOV16_moffset_ax ^ \of
          RT_32 of 32 switch<< COMMAND_DESCRIPTION MOV32_moffset_eax ^ \of
          unknown-operand-type
        \switch
        exit
      then
      // TODO проверить на соответствие data type
      first .regtype @ switch
        RT_8 of COMMAND_DESCRIPTION MOV8_mem_reg ^ \of
        RT_16 of 16 switch<< COMMAND_DESCRIPTION MOV16_mem_reg ^ \of
        RT_32 of 32 switch<< COMMAND_DESCRIPTION MOV32_mem_reg ^ \of
        unknown-operand-type
      \switch
    ;
    : MOV_mem_seg 
      second .value @ DT_WORD = if
        16 switch<< COMMAND_DESCRIPTION MOV16_mem_seg ^
      else " mov from segment register must be 16-bit memory operand" error then
    ;
    : MOV_mem 
      first .type @ switch
        OT_IMM of MOV_mem_imm \of
        OT_REG of MOV_mem_reg \of
        OT_SEG of MOV_mem_seg \of
        " cannot mov from mem to mem"
      \switch
    ;
// }}}
// {{{ MOV seg
    : MOV_seg 
      first .type @ switch
        OT_REG of first .regtype @ RT_16 <> 
                  if " must be 16-bit register in this format" error then 
                  16 switch<< MOV16_seg_reg
        \of
        OT_MEM of first .value @ DT_WORD <> 
                  if " must be 16-bit memory access in this format" error then 
                  16 switch<< MOV16_seg_mem
        \of
        OT_IMM of " cannot mov immediate data to segment register" error \of
        OT_SEG of " cannot mov segment regfister to segment register" error \of
        unknown-operand-type
      \switch
    ;
// }}}
    : MOV
      2 ?operands
      second .type @ switch
        OT_IMM of " dst operand of MOV cannot be immediate " error \of
        OT_REG of MOV_reg \of
        OT_MEM of MOV_mem \of
        OT_SEG of MOV_seg \of
        unknown-operand-type
      \switch clear-operands ;
// }}}
// {{{ MUL
    : MUL_reg
        first .regtype @ switch
          RT_8 of COMMAND_DESCRIPTION MUL8_reg ^ \of
          RT_16 of COMMAND_DESCRIPTION MUL16_reg ^ \of
          RT_32 of COMMAND_DESCRIPTION MUL32_reg ^ \of
          unknown-operand-type
        \switch ;
    : MUL_mem
      first .value @ switch
        DT_BYTE of COMMAND_DESCRIPTION MUL8_mem ^ \of
        DT_WORD of COMMAND_DESCRIPTION MUL16_mem ^ \of
        DT_DWORD of COMMAND_DESCRIPTION MUL32_mem ^ \of
        unknown-operand-type
      \switch ;
    : MUL
        1 ?operands
        first .type @ switch
          OT_REG of MUL_reg \of
          OT_MEM of MUL_mem \of
          unknown-operand-type
        \switch clear-operands ;
// }}}
// {{{ IMUL
    : IMUL_reg
        first .regtype @ switch
          RT_8 of COMMAND_DESCRIPTION IMUL8_reg ^ \of
          RT_16 of COMMAND_DESCRIPTION IMUL16_reg ^ \of
          RT_32 of COMMAND_DESCRIPTION IMUL32_reg ^ \of
          unknown-operand-type
        \switch ;
    : IMUL_reg_reg
      first .regtype @ switch
        RT_16 of COMMAND_DESCRIPTION IMUL16_reg_reg ^ \of
        RT_32 of COMMAND_DESCRIPTION IMUL32_reg_reg ^ \of
        unknown-operand-type
      \switch ;
    : IMUL_reg_mem
      second .regtype @ switch
        RT_16 of COMMAND_DESCRIPTION IMUL16_reg_mem ^ \of
        RT_32 of COMMAND_DESCRIPTION IMUL32_reg_mem ^ \of
        unknown-operand-type
      \switch ;
    : IMUL_reg_*
        first .type @ switch
          OT_REG of IMUL_reg_reg \of
          OT_MEM of IMUL_reg_mem \of
          unknown-operand-type
        \switch ;
    : IMUL_reg_*_*
        first .type @ OT_IMM <> if unknown-operand-type exit then
        second .type @ switch
          OT_REG of unknown-operand-type ( TODO ) \of
          OT_MEM of unknown-operand-type ( TODO ) \of
          unknown-operand-type
        \switch ;
    : IMUL
        operands# switch
          0 of " incorrect number of parameters" error \of
            
          1 of first .type @ OT_REG =
               if IMUL_reg else unknown-operand-type then \of
          2 of second .type @ OT_REG = 
               if IMUL_reg_* else unknown-operand-type then \of
          third .type @ OT_REG =
               if IMUL_reg_*_* else unknown-operand-type then
        \switch clear-operands ;
// }}}
// {{{ LEA
    : LEA_reg16
            first .value @ switch
              DT_WORD of COMMAND_DESCRIPTION LEA16_reg_mem16 ^ \of
              DT_DWORD of COMMAND_DESCRIPTION LEA16_reg_mem32 ^ \of
              unknown-operand-type
            \switch ;
    : LEA_reg32
            first .value @ switch
              DT_WORD of COMMAND_DESCRIPTION LEA32_reg_mem16 ^ \of
              DT_DWORD of COMMAND_DESCRIPTION LEA32_reg_mem32 ^ \of
              unknown-operand-type
            \switch ;
    : LEA
        2 ?operands
        first .type @ OT_MEM <> if unknown-operand-type clear-operands exit then
        second .type @ OT_REG <> if unknown-operand-type clear-operands exit then
        second .regtype @ switch
          RT_16 of LEA_reg16 \of
            // first .value @ switch
            //   DT_WORD of COMMAND_DESCRIPTION LEA16_reg_mem16 ^ \\of
            //   DT_DWORD of COMMAND_DESCRIPTION LEA16_reg_mem32 ^ \\of
            //   unknown-operand-type
            // \\switch \\of
          RT_32 of LEA_reg32 \of
            // first .value @ switch
            //   DT_WORD of COMMAND_DESCRIPTION LEA32_reg_mem16 ^ \\of
            //   DT_DWORD of COMMAND_DESCRIPTION LEA32_reg_mem32 ^ \\of
            //   unknown-operand-type
            // \\switch \\of
          unknown-operand-type
        \switch clear-operands ;
// }}}
// {{{ template for binary command
" 
    : <INSTRUCTION>_reg_imm
      second .value @ EAX = if
        second .regtype @ switch
          RT_8  of COMMAND_DESCRIPTION <INSTRUCTION>8_al_imm ^ \of
          RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_ax_imm ^ \of
          RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_eax_imm ^ \of
          unknown-operand-type
        \switch
      else
        first .value @ 256 < if
          second .regtype @ switch
            RT_8  of COMMAND_DESCRIPTION <INSTRUCTION>8_reg_imm ^ \of
            RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_reg_imm8 ^ \of
            RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_reg_imm8 ^ \of
            unknown-operand-type
          \switch
        else
          second .regtype @ switch
            RT_8  of COMMAND_DESCRIPTION <INSTRUCTION>8_reg_imm ^ \of
            RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_reg_imm ^ \of
            RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_reg_imm ^ \of
            unknown-operand-type
          \switch
        then
      then
    ;
    : <INSTRUCTION>_reg_reg
      first .regtype @ second .regtype @ <> 
      if ^" both operands of <INSTRUCTION> must have the same type^" error then
      second .regtype @ switch
        RT_8 of COMMAND_DESCRIPTION <INSTRUCTION>8_reg_reg ^ \of
        RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_reg_reg ^ \of
        RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_reg_reg ^ \of
        unknown-operand-type
      \switch
    ;
    : <INSTRUCTION>_reg_mem
      second .regtype @ switch
        RT_8  of COMMAND_DESCRIPTION <INSTRUCTION>8_reg_mem ^ \of
        RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_reg_mem ^ \of
        RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_reg_mem ^ \of
        unknown-operand-type
      \switch
    ;
    : <INSTRUCTION>_reg
      first .type @ switch
        OT_IMM of <INSTRUCTION>_reg_imm \of
        OT_REG of <INSTRUCTION>_reg_reg \of
        OT_MEM of <INSTRUCTION>_reg_mem \of
        unknown-operand-type
      \switch
    ;
    : <INSTRUCTION>_mem_imm
        first .value @ 256 < if
          second .value @ switch
            DT_BYTE of COMMAND_DESCRIPTION <INSTRUCTION>8_mem_imm ^ \of
            DT_WORD of COMMAND_DESCRIPTION <INSTRUCTION>16_mem_imm8 ^ \of
            DT_DWORD of COMMAND_DESCRIPTION <INSTRUCTION>32_mem_imm8 ^ \of
            unknown-operand-type
          \switch
        else
          second .value @ switch
            DT_BYTE of COMMAND_DESCRIPTION <INSTRUCTION>8_mem_imm ^ \of
            DT_WORD of COMMAND_DESCRIPTION <INSTRUCTION>16_mem_imm ^ \of
            DT_DWORD of COMMAND_DESCRIPTION <INSTRUCTION>32_mem_imm ^ \of
            unknown-operand-type
          \switch
        then ;
    : <INSTRUCTION>_mem_reg 
      first .regtype @ switch
        RT_8 of COMMAND_DESCRIPTION <INSTRUCTION>8_mem_reg ^ \of
        RT_16 of COMMAND_DESCRIPTION <INSTRUCTION>16_mem_reg ^ \of
        RT_32 of COMMAND_DESCRIPTION <INSTRUCTION>32_mem_reg ^ \of
        unknown-operand-type
      \switch ;
    : <INSTRUCTION>_mem 
      first .type @ switch
        OT_IMM of <INSTRUCTION>_mem_imm \of
        OT_REG of <INSTRUCTION>_mem_reg \of
        OT_MEM of ^" cannot mov from mem to mem^" error \of
        unknown-operand-type
      \switch ;
    : <INSTRUCTION>
          2 ?operands
          second .type @ switch
            OT_IMM of ^" dst operand of <INSTRUCTION> cannot be immediate ^" error \of
            OT_REG of <INSTRUCTION>_reg \of
            OT_MEM of <INSTRUCTION>_mem \of
            unknown-operand-type
          \switch clear-operands ;
"
// }}}
: delta. ( start) timer swap - . ; 
bdup " <INSTRUCTION>" \" ADD" evaluate ." ADD done" cr
bdup " <INSTRUCTION>" \" OR" evaluate ." OR done" cr
bdup " <INSTRUCTION>" \" ADC" evaluate ." ADC done" cr
bdup " <INSTRUCTION>" \" SBB" evaluate ." SBB done" cr
bdup " <INSTRUCTION>" \" AND" evaluate ." AND done" cr
bdup " <INSTRUCTION>" \" SUB" evaluate ." SUB done" cr
bdup " <INSTRUCTION>" \" XOR" evaluate ." XOR done" cr
bdup " <INSTRUCTION>" \" CMP" evaluate ." CMP done" cr

    : _COMPILE_NOTFOUND_
        *auto-integers? @ dup if str->int postpone literal postpone imm 
                              else bdrop then
    ;
    : _NOTFOUND_ // false bdrop exit
        // str-nospace if true exit then
        *auto-integers? @ dup if str->int imm else bdrop then
    ;    

    
    PREFIX 
      : PREFIX call PREFIX ['] nop PREFIX *xt ^ ! ;
      ' ADC prefix-mnemonic adc
      ' ADD prefix-mnemonic add
      ' AND prefix-mnemonic and
      ' CMP prefix-mnemonic cmp
      ' DEC prefix-mnemonic dec
      ' IMUL prefix-mnemonic imul
      ' INC prefix-mnemonic inc
      ' LEA prefix-mnemonic lea
      ' MOV prefix-mnemonic mov
      ' MUL prefix-mnemonic mul
      ' NEG prefix-mnemonic neg
      ' NOP prefix-mnemonic nop
      ' OR prefix-mnemonic or
      ' POP prefix-mnemonic pop
      ' PUSH prefix-mnemonic push
      ' RET prefix-mnemonic ret
      ' SBB prefix-mnemonic sbb
      ' SUB prefix-mnemonic sub
      ' XCHG prefix-mnemonic xchg
      ' XOR prefix-mnemonic xor
    ^
  \namespace
\uses
\namespace
